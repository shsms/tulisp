WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ ";" ~ (!"\n" ~ ANY)* }

alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
single_quote = _{ "'" }
backtick = _{ "`"}
comma = _{","}
splice_oper = _{ ",@" }
minus = { "-" }

symbol = _{ underscore | colon | ampersand | pipe | bang | at | dollar | question | equal | dot | plus | star | slash | cap | percent | gt | lt}
underscore = { "_" }
colon = { ":" }
ampersand = { "&" }
pipe = { "|" }
bang = { "!" }
at = { "@" }
dollar = { "$" }
question = { "?" }
equal = { "=" }
dot = _{ "." }
plus = { "+" }
star = { "*" }
slash = { "/" }
cap = { "^" }
percent = { "%" }
gt = { ">" }
lt = { "<" }

open = _{ "(" }
close = _{ ")" }

quote = { single_quote ~ expr}
backquote = { backtick ~ expr}
splice = { splice_oper ~ expr}
unquote = { comma ~ expr}

integer = @{ minus? ~ digit+ }
float = @{ (integer ~ dot ~ digit+) | (integer ~ ^"e" ~ integer) }
number = _{ float | integer }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = { !("\"") ~ ANY }

literal = _{ number | string }
ident = @{ (alpha | symbol) ~ (alpha | digit | minus | symbol)* |
    (number | minus) ~ (alpha | digit | minus | symbol)+ |
    minus }

cons = { open ~ expr ~ dot ~ expr ~ close }

form = { open ~ (expr)* ~ close }

nil = { "nil" }
expr = _{ nil | literal | cons | form  | quote | backquote | splice | unquote  | ident }

program = _{ SOI ~ (expr)+ }
